COMPILER Tastier

   const int // object kinds
      var = 0, proc = 1, scope = 2, constant = 3;

    
   const int // variable sort 
      scalar = 1, array = 2;
    

   const int // types
      undef = 0, integer = 1, boolean = 2;

   public SymbolTable tab;
   public CodeGenerator gen;
  
/*-------------------------------------------------------------------------------------------*/

CHARACTERS

  cr  = '\r'.
  lf  = '\n'.
  tab = '\t'.
  digit = "0123456789".
  letter = "ABCDEFGHIJKLMNOPQRSTUVWXYZabcdefghijklmnopqrstuvwxyz".
  stringCh = ANY - '"' - '\\' - cr - lf - tab.
  printableCh = '\u0020' .. '\u007e'.

/*-------------------------------------------------------------------------------------------*/

TOKENS

  number = digit { digit }.
  ident = letter { letter | digit }.
  string = '"' {stringCh | '\\' printableCh} '"'.

/*-------------------------------------------------------------------------------------------*/

COMMENTS FROM "//" TO lf
COMMENTS FROM "/*" TO "*/" NESTED

/*-------------------------------------------------------------------------------------------*/

IGNORE cr + lf + tab

/*-------------------------------------------------------------------------------------------*/

PRODUCTIONS

AddOp<out Op op>
=                        (. op = Op.ADD; .)
  ( '+'
  | '-'                  (. op = Op.SUB; .)
  )
.

/*-------------------------------------------------------------------------------------------*/

Expr<out int reg,        // load value of Expr into register
     out int type>       (. int typeR, regR; Op op; .)
  = SimExpr<out reg,
            out type>
  [ RelOp<out op>
    SimExpr<out regR,
            out typeR>   (.
                            if (type == typeR) {
                               type = boolean;
                               gen.RelOp(op, reg, regR);
                            }
                            else SemErr("incompatible types");
                         .)
   ]                     (. gen.ClearRegisters(); .)
.

/*-------------------------------------------------------------------------------------------*/

Primary<out int reg,     // load Primary into register
        out int type>    (. int n; Obj obj; string name; .)
=                        (.
                            type = undef;
                            reg = gen.GetRegister();
                         .)
  ( Ident<out name>      (.
                            obj = tab.Find(name); type = obj.type;
                            if (obj.kind == var) {
                               if (obj.level == 0)
                                  gen.LoadGlobal(reg, obj.adr, name);
                               else
                                  gen.LoadLocal(reg, tab.curLevel-obj.level, obj.adr, name);
                               if (type == boolean)
                               // reset Z flag in CPSR
                                  gen.ResetZ(reg);
                            }
                            else SemErr("variable expected");
                         .)

  | number               (.
                            type = integer;
                            n = Convert.ToInt32(t.val);
                            gen.LoadConstant(reg, n);
                         .)

  | '-'
    Primary<out reg,
            out type>     (.
                            if (type == integer)
                               gen.NegateValue(reg);
                            else SemErr("integer type expected");
                         .)

  | "true"               (.
                               type = boolean;
                               gen.LoadTrue(reg);
                         .)

  | "false"              (.
                               type = boolean;
                               gen.LoadFalse(reg);
                         .)

  | '('
    Expr<out reg,
         out type>
    ')'

  )
.

/*-------------------------------------------------------------------------------------------*/

Ident<out string name>
= ident                  (. name = t.val; .)
.

/*-------------------------------------------------------------------------------------------*/

String<out string text>
= string                  (. text = t.val; .)
.

/*-------------------------------------------------------------------------------------------*/

MulOp<out Op op>
=                        (. op = Op.MUL; .)
  ( '*'
  | ("div" | "DIV")      (. op = Op.DIV; .)
  | ("mod" | "MOD")      (. op = Op.MOD; .)
  )
.

/*-------------------------------------------------------------------------------------------*/

ProcDecl<string progName>
                         (. Obj obj; string procName; .)
= "void"
  Ident<out procName>    (.
                            obj = tab.NewObj(procName, proc, undef, undef, undef);
                            if (procName == "main")
                               if (tab.curLevel == 0)
                                  tab.mainPresent = true;
                               else SemErr("main not at lexic level 0");
                            tab.OpenScope();
                         .)
  '(' ')'
  '{'
     {
     VarDecl
     }
     {
     ProcDecl<progName>
     }                   (.
                            if (procName == "main")
                               gen.Label("Main", "Body");
                            else {
                               gen.ProcNameComment(procName);
                               gen.Label(procName, "Body");
                            }
                         .)
     Stat { Stat }
  '}'                    (.
                            if (procName == "main") {
                               gen.StopProgram(progName);
                               gen.Enter("Main", tab.curLevel, tab.topScope.nextAdr);
                            } else {
                               gen.Return(procName);
                               gen.Enter(procName, tab.curLevel, tab.topScope.nextAdr);
                            }
                            tab.CloseScope();
                         .)
.

/*-------------------------------------------------------------------------------------------*/

RelOp<out Op op>
=                        (. op = Op.EQU; .)
  ( "="
  | '<'                  (. op = Op.LSS; .)
  | '>'                  (. op = Op.GTR; .)
  | "!="                 (. op = Op.NEQ; .)
  | "<="                 (. op = Op.LEQ; .)
  | ">="                 (. op = Op.GEQ; .)
  )
.

/*-------------------------------------------------------------------------------------------*/

SimExpr<out int reg,     //load value of SimExpr into register
        out int type>    (. int typeR, regR; Op op; .)
= Term<out reg,
       out type>
  { AddOp<out op>
    Term<out regR,
         out typeR>      (.
                            if (type == integer && typeR == integer)
                               gen.AddOp(op, reg, regR);
                            else SemErr("integer type expected");
                         .)
  }
.

/*-------------------------------------------------------------------------------------------*/

/* added assignment to array elem */
Stat                     (. int type; int sort; string name; Obj obj, obj1; int reg; int reg1; int n; .)
= 
  Ident<out name>        (. obj = tab.Find(name); n = 0; reg1 = gen.GetRegister(); .)
  (
   [
      "["
      ( 
        number                (.
                            n = Convert.ToInt32(t.val);
                            if( n >= 0 && n < (obj.sizeArray-1) )
                              gen.LoadConstant(reg1, n);
                            else
                              SemErr("Array index is out of bounds");
                          .) 
        |

        Ident<out name>        (. obj1 = tab.Find(name); 
                                  type = obj1.type;
                                  if (obj.kind == var && type == integer) {
                                     if (obj.level == 0)
                                        gen.LoadGlobal(reg1, obj1.adr, name);
                                     else
                                        gen.LoadLocal(reg1, tab.curLevel-obj1.level, obj1.adr, name);
                                  }
                                  else
                                      SemErr("Integer variable expected.");   .) 
      )

     "]"
   ]      
   ":="                 (.
                            if ((obj.kind == var || (obj.kind == constant && !obj.constAssigned ) ))
                                obj.constAssigned = true;
                            else
                            
                                SemErr("cannot assign to non variable");
                         .)
    Expr<out reg,
         out type> ';'   (.
                            if (type == obj.type )
                            {
                                if(obj.sort == scalar)
                                {
                                    if(obj.level == 0)
                                        gen.StoreGlobal(reg, obj.adr, name);
                                    else gen.StoreLocal(reg, tab.curLevel-obj.level, obj.adr, name);
                                }
                                else if(obj.sort == array)
                                {
                                    if(obj.level == 0)
                                        gen.StoreIndexedGlobal(reg, obj.adr, reg1, name);
                                    else gen.StoreIndexedLocal(reg, tab.curLevel-obj.level, obj.adr, reg1, name);
                                }
                            }
                               
                         .)
       
     
  | 
  '(' ')' ';'          (.
                            if (obj.kind == proc)
                               gen.Call(name);
                            else SemErr("object is not a procedure");
                         .)

  
)
| "if"                   (. int l1, l2; l1 = 0; .)
  Expr<out reg,
       out type>         (.
                            if (type == boolean) {
                               l1 = gen.NewLabel();
                               gen.BranchFalse(l1);
                            }
                            else SemErr("boolean type expected");
                         .)
      Stat               (.
                            l2 = gen.NewLabel();
                            gen.Branch(l2);
                            gen.Label(l1);
                         .)
      [ "else"
      Stat
      ]               	 (. gen.Label(l2); .)

| "while"                (.
                            int l1, l2;
                            l1 = gen.NewLabel();
                            gen.Label(l1); l2=0;
                         .)
  Expr<out reg,
       out type>         (.
                            if (type == boolean) {
                               l2 = gen.NewLabel();
                               gen.BranchFalse(l2);
                            }
                            else SemErr("boolean type expected");
                         .)
   Stat                  (.
                            gen.Branch(l1);
                            gen.Label(l2);
                         .)

| "for"                   (.
                            int l1, l2, l3;
                            l1 = gen.NewLabel();
                             l2 = gen.NewLabel();
                             l3 = 0;
                          .)
  '('
  Ident<out name>         (. obj = tab.Find(name); .)
  ":="                   (.
                            if (obj.kind != var | obj.sort != scalar)
                               SemErr("Expecting scalar variable assignment");
                         .)
    Expr<out reg,
         out type> ';'   (.
                            if (type == obj.type )
                            {
                                if(obj.sort == scalar)
                                {
                                    if(obj.level == 0)
                                        gen.StoreGlobal(reg, obj.adr, name);
                                    else gen.StoreLocal(reg, tab.curLevel-obj.level, obj.adr, name);
                                }
                            }
                            gen.Branch(l1);
                            gen.Label(l2);
                          .)


    Ident<out name>         (. obj1 = tab.Find(name); .)
    ":="                   (.
                            if (obj1.kind != var | obj1.sort != scalar)
                               SemErr("Expecting scalar variable assignment");
                         .)

    Expr<out reg,
         out type>     (.
                            if (type == obj1.type )
                            {
                                if(obj1.sort == scalar)
                                {
                                    if(obj1.level == 0)
                                        gen.StoreGlobal(reg, obj1.adr, name);
                                    else gen.StoreLocal(reg, tab.curLevel-obj1.level, obj1.adr, name);
                                }
                            }
                            gen.Label(l1);
                         .)   

    Expr<out reg,
         out type> ';'    (.
                            if (type == boolean) {
                               l3 = gen.NewLabel();
                               gen.BranchFalse(l3);
                            }
                            else SemErr("boolean type expected");
                         .)

    ')'

    Stat                  (.
                            gen.Branch(l2);
                            gen.Label(l3);
                         .)



| "read"
  Ident<out name> ';'    (.
                            obj = tab.Find(name);
                            if (obj.type == integer) {
                               gen.ReadInteger(); 
                               if (obj.level == 0)
                                  gen.StoreGlobal(0, obj.adr, name);
                               else gen.StoreLocal(0, tab.curLevel-obj.level, obj.adr, name);
                            }
                            else SemErr("integer type expected");
                         .)

| "write"                (. string text; .)
  (
    Expr<out reg,
         out type>       (.
                            switch (type) {
                               case integer: gen.WriteInteger(reg, false);
                                             break; 
                               case boolean: gen.WriteBoolean(false);
                                             break;
                            }
                         .)
  | String<out text>     (. gen.WriteString(text); .)
  )
  ';'

| "writeln"
  // note - for strings use write with \n in text
  Expr<out reg,
       out type>           (.
                              switch (type) {
                                 case integer: gen.WriteInteger(reg, true);
                                               break;
                                 case boolean: gen.WriteBoolean(true);
                                               break;
                              }
                           .)
  ';'

| '{'                      (. tab.OpenSubScope(); .)
  { VarDecl }
  Stat { Stat }
  '}'                      (. tab.CloseSubScope(); .)
.

/*-------------------------------------------------------------------------------------------*/

Tastier                  (. string progName; .)
  = "program"
  Ident<out progName>    (. tab.OpenScope(); .)
  '{'
    { VarDecl | ConstDecl}          // global variables
    { ProcDecl<progName> }
                         (. tab.CloseScope(); .)
  '}'
.

/*-------------------------------------------------------------------------------------------*/

Term<out int reg,        // load value of Term into register
     out int type>       (. int typeR, regR; Op op; .)
  = Primary<out reg,
            out type>
  { MulOp<out op>
    Primary<out regR,
            out typeR>
                         (.
                            if (type == integer && typeR == integer)
                               gen.MulOp(op, reg, regR);
                            else SemErr("integer type expected");
                         .)
  }
.

/*-------------------------------------------------------------------------------------------*/

Type<out int type>
=                        (. type = undef; .)
 ( "int"                 (. type = integer; .)
 | "bool"                (. type = boolean; .)
 )
.

/*-------------------------------------------------------------------------------------------*/

/*I added array decl possibility*/
VarDecl                  (. string name; int type; int n; .)
= Type<out type>
  (
    Ident<out name>        (. tab.NewObj(name, var, type, scalar, undef); .)
    { ","
      Ident<out name>      (. tab.NewObj(name, var, type, scalar, undef); .)
    } ";"
  |
    "["
    number                 (.
                            //type = integer;
                            n = Convert.ToInt32(t.val);
                            //gen.LoadConstant(reg, n);
                          .)
    "]"
    Ident<out name>        (. tab.NewObj(name, var, type, array, n); .)
    ";"
  )

.

/*-------------------------------------------------------------------------------------------*/

ConstDecl                  (. string name; int type; .)
= "const"
  Type<out type>
  Ident<out name>        (. tab.NewObj(name, constant, type, undef, undef); .)
  { ","
    Ident<out name>      (. tab.NewObj(name, constant, type, undef, undef); .)
  } ";"
.

END Tastier.
